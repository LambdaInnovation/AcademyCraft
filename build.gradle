//This build script can upload built jars to a maven repo.
//
//Usage:
//  Use task 'uploadGithubAndClean'. It will build, generate jars and publish them.
//  gradle params:
//    -PnewMavenRepo=1
//      Create a new maven repo, instead of reading from Github.
//    -PpublishVersion=<string>
//      Use the given string as the project version.
//    -Dorg.ajoberstar.grgit.auth.username=<string>
//      The username in authentication to Github repo.
//    -Dorg.ajoberstar.grgit.auth.password=<string>
//      The password in authentication to Github repo.

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forgemaven"
            url = "https://raw.github.com/LambdaInnovation/ForgeMaven/mvn-repo"
        }
    }
    dependencies {
        classpath 'cn.li:ForgeMaven:1.0'
    }
}

apply plugin: 'cn.li.forgemaven'

forgemaven {
    name = "AcademyCraft"
    group = "cn.academy"

    forgeVersion = "1.7.2-10.12.2.1121"
    gitUrl = 'https://github.com/LambdaInnovation/AcademyCraft.git'

    jarClasses {
        exclude 'cn/annoreg/**/*'
        exclude 'cn/liutils/**/*'
    }

    gitUser {
        username = 'LI Github'
        email = 'github@li-dev.cn'
    }

    pom {
        groupId = project.group
        version = project.version
        artifactId = project.archivesBaseName
        project {
            name project.archivesBaseName
            packaging 'jar'
            description 'A Minecraft mod about superability, including its aquiring, upgrading and using.'
            url 'https://github.com/LambdaInnovation/AcademyCraft'

            issueManagement {
                system 'github'
                url 'https://github.com/LambdaInnovation/AcademyCraft/issues'
            }

            licenses {
                license {
                    name 'GNU General Public License.'
                    url 'http://www.gnu.org/licenses/gpl.html'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'LambdaInnovation'
                    name 'LambdaInnovation'
                    roles { role 'developer' }
                }
            }
        }
    }
}

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name = "annoreg"
        url = "https://raw.github.com/LambdaInnovation/AnnotationRegistry/mvn-repo/AnnotationRegistry"
    }
    maven {
        name = "liutils"
        url = "https://raw.github.com/LambdaInnovation/LIUtils/mvn-repo/LIUtils"
    }
}

dependencies {
    //dependencies needed in compilation
    modCompile 'net.industrial-craft:industrialcraft-2:2.1.484-experimental:dev'
    modCompile 'cn.annoreg:AnnotationRegistry:[1.7.2.1,1.7.3.0):sources'
    modCompile 'cn.liutils:LIUtils:[1.7.2.1,1.7.3.0):sources'
}

excludeDepJar {
    manifest {
        attributes 'FMLCorePlugin': 'cn.academy.core.ACCorePlugin'
    }
}
